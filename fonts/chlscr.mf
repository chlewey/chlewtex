mode_setup;
	em#:=10pt#;
	ex#:=3pt#;
	cap#:=7pt#;
	thin#:=1/3pt#;
	thick#:=5/6pt#;
	o#:=1/5pt#;
	u#:=4/9pt#;
	mg#=4/5pt#;
	el#:=15/16cap#;
define_pixels(em,ex,cap,u,mg,el);
	font_size em#;
	font_slant 0;
	font_normal_space 1/3em#;
	font_normal_stretch 1/5em#;
	font_normal_shrink 1/6em#;
	font_x_height ex#;
	font_quad em#;
define_blacker_pixels(thin,thick);
define_corrected_pixels(o);
	curve_sidebar=round 1/18em;
def offset(expr dx,dy)=(1/3dx*normaldeviate,1/3dy*normaldeviate)enddef;
def offsetr(expr d)=offset(d,d)enddef;
def offsetx(expr d)=offset(1/2d,1/4d)+(d,0)enddef;
def offsety(expr d)=offset(1/4d,1/2d)+(0,d)enddef;
def vert(expr a,b,c)=flex((a,b)+offsetx(2/5u),(a,2/5[c,b]),(a,c)+offsetx(2/5u))enddef;
def horz(expr a,b,c)=flex((a,c)-offsety(1/2u),(1/2[a,b],c),(b,c)+offsetr(1/4u)) enddef;
def diag(expr z,w)=
	flex(z,1/2[z,w]+offset(1/2u,1/3u),w) enddef;
def zigii(expr z,w,v)=
	flex(z,1/2[z,w]+offset(1/2u,1/3u),w) &
	flex(w,1/2[w,v]+offset(1/2u,1/3u),v) enddef;
def zigiii(expr z,w,v,t)=
	flex(z,1/2[z,w]+offset(1/2u,1/3u),w) &
	flex(w,1/2[w,v]+offset(1/2u,1/3u),v) &
	flex(v,1/2[v,t]+offset(1/2u,1/3u),t) enddef;
def dot(expr a,b)=(a-1/15u,b+1/12u)..(a+1/20u*normaldeviate,b+1/20u*normaldeviate)..(a+1/15u,b-1/12u) enddef;
def beginCchar(expr code,wd)=beginchar(code,wd*u#+2mg#,cap#,0);"The letter "&code;
	z1=(5u,1ex);
	z2=(0,1/3ex);
	z3=(4u,0);
	z4=(9u,1/2ex);
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
enddef;
def beginachar(expr code,wd)=beginchar(code,wd*u#+2mg#,ex#,0);"The letter "&code;
	z1=(5u,1ex);
	z2=(0,1/3ex);
	z3=(4u,0);
	z4=(9u,1/2ex);
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
enddef;
def beginbchar(expr code,wd)=beginchar(code,wd*u#+2mg#,el#,0);"The letter "&code;
	z1=(5u,1ex);
	z2=(0,1/3ex);
	z3=(4u,0);
	z4=(9u,1/2ex);
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
enddef;
def beginpchar(expr code,wd,dp)=beginchar(code,wd*u#+2mg#,ex#,dp*ex#);"The letter "&code;
	z1=(5u,1ex);
	z2=(0,1/3ex);
	z3=(4u,0);
	z4=(9u,1/2ex);
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
enddef;
def beginXchar(expr code,wd,ht,dp)=beginchar(code,wd*u#+2mg#,ht*ex#,dp*ex#);"The letter "&code;
	z1=(5u,1ex);
	z2=(0,1/3ex);
	z3=(4u,0);
	z4=(9u,1/2ex);
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
enddef;
def beginnum(expr code)=beginchar(code,9u#+2mg#,el#,0);"The number "&code;
	z1=(5u,1ex);
	z2=(0,1/3ex);
	z3=(4u,0);
	z4=(9u,1/2ex);
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
enddef;
def beginsymbol(expr code,wd,ht,dp)=beginchar(code,wd*u#+2mg#,ht,dp*ex#);"The symbol "&code;
	z1=(5u,1ex);
	z2=(0,1/3ex);
	z3=(4u,0);
	z4=(9u,1/2ex);
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
enddef;
def endxchar=
	labels(range 1 thru 4);
	currentpicture := currentpicture shifted (2u,0);
endchar;enddef;
beginchar(" ",1/3em#,cap#,0);"The space";
endchar;
beginsymbol("!",1,em#,0);
	draw vert(0,cap,.2cap);
	draw dot(0.5u,-.05cap);
endxchar;
beginsymbol(",",0,0,1/5);
	draw dot(0,0){down}..(-u,-1/5ex);
endxchar;
beginsymbol(".",0,0,0);
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
	draw dot(0,0);
endxchar;
beginnum("0");
	draw (5u,el){left}..(0,4/9el){down}..(4u,0){right}..(9u,5/9el){up}..(4.5u,el);
endxchar;
beginnum("1");
	draw (u,.7el)..(5u,el) & diag((5u,el),(4.5u,0));
	draw horz(0,9u,0);
endxchar;
beginnum("2");
	draw (u,.8el)..(5u,el){right}..(9u,7/9el){down}..{dir -120}(0,1/15el){dir -15}..(8u,0);
endxchar;
beginnum("3");
	draw (u,8/9el)..(6u,el){right}..(9u,8/9el){down}..{dir -150}(5u,5/9el){dir -45}..(9u,2/9el){down}..(4u,0){left}..(0,.1el);
endxchar;
beginnum("4");
	draw (2u,8/9el)..(0,5/9el){down}..{right}(u,4/9el)..(7u,1/2el);
	draw (9u,el)..{down}(8u,0);
endxchar;
beginnum("5");
	draw (4u,.8el)..{down}(3u,.5el)..z1{right}..z4{down}..{left}(0,0);
	draw (2u,.85el){right}..(9u,el);
endxchar;
beginnum("6");
	draw (8u,el)..z2{down}..z3{right}..z4{up}..z1{left};
endxchar;
beginnum("7");
	draw zigiii((.5u,el),(0,.8el),(u,.9el),(8u,el)){right}..(9u,.9el)..{down}(3u,0);
endxchar;
beginnum("8");
	draw (4u,el)..(z2+(u,el-ex)){down}..z4{down}..z3{left}..z2{up}..(z4-(u,ex-el)){up}..(5u,el);
endxchar;
beginnum("9");
	draw (9u,3/4ex){dir 105}..z1{left}..z2{down}..z3{right}..(8u,1/6ex)--(8u,1/6ex){down}..(9u,ex-el);
	currentpicture := currentpicture shifted (0,el-ex);
endxchar;
beginchar(":",1/3em#,ex#,0);"The symbol :";
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
	draw dot(2u,ex);
	draw dot(2u,0);
endchar;
beginchar(";",1/3em#,ex#,0);"The symbol ;";
	pickup pencircle xscaled 1.3u yscaled u rotated 30;
	draw dot(2u,ex);
	draw dot(2u,0){down}..(u,-1/5ex);
endchar;
beginCchar("?",9);
	draw (u,.8cap)..(5u,cap){right}..(9u,7/9cap){down}..{down}(4u,1/5cap){dir -15};
	draw dot(4u,-.05cap);
endxchar;
beginCchar("A",12);
	draw zigii((0,0),(6.5u,cap),(12u,0));
	draw horz(2.5u,10.5u,.7ex);
endxchar;
beginCchar("B",9);
	draw vert(0,cap,0);
	draw (0,cap){right}..(8u,11/15cap)..{left}(u,7/15cap){right}..(9u,3/15cap)..{left}(u,0);
endxchar;
beginCchar("C",11);
	draw (11u,.9cap){dir 105}..(6u,cap){left}..(0,.4cap){down}..(5u,0){right}..(11u,.1cap);
endxchar;
beginCchar("D",9);
	draw vert(0,cap,0);
	draw (0,cap){right}..(9u,7/15cap)..{left}(u,0);
endxchar;
beginCchar("E",10);
	draw vert(0,cap,0){right}..(10u,.05cap);
	draw horz(u,8u,.5cap);
	draw horz(0,9u,cap);
endxchar;
beginCchar("F",9);
	draw vert(0,cap,0);
	draw horz(u,8u,.5cap);
	draw horz(0,9u,cap);
endxchar;
beginCchar("G",11);
	draw (11u,.9cap){dir 105}..(6u,cap){left}..(0,.4cap){down}..(5u,0){right}..(10u,.2cap);
	draw zigii((5u,.4cap),(11u,.45cap),(10.5u,.1cap));
endxchar;
beginCchar("H",10);
	draw vert(0,cap,0);
	draw horz(u,10u,.5cap);
	draw vert(10u,el,-1/8ex);
endxchar;
beginCchar("I",4);
	draw vert(2u,cap,0);
	draw horz(0,4u,cap);
	draw horz(0,4u,0);
endxchar;
beginCchar("J",4);
	draw vert(4u,cap,ex)...(4u,1/6ex)..(-u,-1/6ex);
endxchar;
beginCchar("K",10);
	draw vert(0,cap,0);
	draw diag((0,2/5cap),(9u,cap));
	draw diag((3u,3/5cap),(10u,0));
endxchar;
beginCchar("L",10);
	draw vert(0,cap,0){right}..(10u,.05cap);
endxchar;
beginCchar("M",12);
	draw vert(0,cap,0);
	draw zigiii((0,cap),(6u,1/3cap),(11.5u,cap),(12u,0));
endxchar;
beginCchar("N",9);
	draw vert(0,cap,0);
	draw zigii((0,cap),(8u,0),(9u,el));
endxchar;
beginCchar("O",12);
	draw (6u,cap)..(0,.4cap){down}..(5u,0){right}..(12u,.5cap){up}..(6u,cap);
endxchar;
beginCchar("P",8);
	draw vert(0,cap,0);
	draw (0,cap){right}..(8u,11/15cap)..{left}(u,7/15cap);
endxchar;
beginCchar("Q",12);
	draw (6u,cap)..(0,.4cap){down}..(5u,0){right}..(12u,.5cap){up}..(6u,cap);
	draw diag((6u,1/3ex),(13u,-1/8ex));
endxchar;
beginCchar("R",9);
	draw vert(0,cap,0);
	draw (0,cap){right}..(8u,11/15cap)..{left}(u,7/15cap) & diag((u,7/15cap),(9u,0));
endxchar;
beginCchar("S",9);
	draw (8u,el)..(4.5u,cap){left}..(0,11/14cap){down}..(9u,4/14cap){down}..(4u,0)..(0,1/14cap);
endxchar;
beginCchar("T",12);
	draw vert(6u,cap,0);
	draw horz(0,13u,cap);
endxchar;
beginCchar("U",10);
	draw (0,cap)..(0,.3cap)..(5u,0)..(10u,.35cap)..(10u,cap);
endxchar;
beginCchar("V",12);
	draw zigii((0,cap),(6u,0),(12u,el));
endxchar;
beginCchar("W",12);
	draw zigii((-.5u,cap),(3u,0),(6u,.7cap)) & zigii((6u,.7cap),(9u,0),(12.5u,el));
endxchar;
beginCchar("X",12);
	draw diag((u,cap),(11.5u,0));
	draw diag((12u,cap),(0,0));
endxchar;
beginCchar("Y",10);
	draw diag((u,cap),(4.5u,.5cap));
	draw zigii((10u,el),(5.5u,.45cap),(5u,0));
endxchar;
beginCchar("Z",16);
	labels(range 1 thru 4);
	draw zigiii((2u,cap),(11u,el),(2u,0),(12u,1/6ex));
endchar;
beginachar("a",9);
	draw (9u,3/4ex){dir 105}..z1{left}..z2{down}..z3{right}..(8u,1/6ex)&(8u,1/6ex){down}..(7.5u,-1/8ex);
endxchar;
beginbchar("b",9);
	draw (u,el)..{down}z2--z2..z1{right}..z4{down}..z3{left};
endxchar;
beginachar("c",9);
	draw (9u,3/4ex){dir 105}..z1{left}..z2{down}..z3{right}..(8u,1/6ex);
endxchar;
beginbchar("d",9);
	draw (8.5u,el)..{down}(9u,3/4ex){dir 105}..z1{left}..z2{down}..z3{right}..(8u,1/6ex)--(8u,1/6ex){down}..(7.5u,-1/8ex);
endxchar;
beginachar("e",9);
	draw (2u,1/2ex){right}..{up}(9u,3/4ex)..z1{left}..z2{down}..z3{right}..(8u,1/6ex);
endxchar;
beginXchar("f",9,1.5,.5);
	draw (11u,7/6ex){up}..(2u,0){down}..(0,-1/3ex);
	draw (0,1/6ex){right}..(5u,1/5ex);
endxchar;
beginpchar("g",9,.8);
	draw (9u,3/4ex){dir 105}..z1{left}..z2{down}..z3{right}..(8u,1/6ex)--(8u,1/6ex){down}..(-2u,-1/2ex);
endxchar;
beginbchar("h",8.5);
	draw (u,el)..{down}z2--z2..z1{right}..z4{down}..(8.5u,-1/8ex);
endxchar;
beginXchar("i",.5,7/5,0);
	draw (u,ex)..{down}(u,0);
	draw dot(4/3u,7/5ex);
endxchar;
beginXchar("j",2,7/5,.5);
	draw (2u,ex)..{down}(u,0)..(-2u,-1/2ex);
	draw dot(7/3u,7/5ex);
endxchar;
beginbchar("k",9);
	draw (u,el)..{down}z2--(0,0)..{right}(7u,ex)--(7u,ex){left}..(9u,0){right};
endxchar;
beginbchar("l",1);
	draw (u,el)..{down}z2..(.5u,-1/8ex);
endxchar;
beginachar("m",12);
	draw (.5u,9/8ex)..{down}z2--z2..z1{right}..{down}(7u,2/3ex)&(7u,2/3ex)..(9u,ex){right}..(12u,0){down};
endxchar;
beginachar("n",9);
	draw (.5u,9/8ex)..{down}z2--z2..z1{right}..(9u,0){down};
endxchar;
beginachar("o",9);
	draw z1..z2{down}..z3{right}..z4{up}..z1;
endxchar;
beginpchar("p",9,4/5);
	draw (.5u,9/8ex)..z2---(0,-.8ex)--z2..z1{right}..z4{down}..z3{left};
endxchar;
beginpchar("q",9,2/3);
	draw (9u,2/3ex){up}..z1{left}..z2{down}..z3{right}..(8u,1/6ex)--(8u,1/6ex){down}..(7.5u,-2/3ex);
	draw (3u,-1/2ex){right}..(11u,-1/3ex);
endxchar;
beginachar("r",7);
	draw (.5u,9/8ex)..{down}z2--z2..z1{right}..(7u,.9ex);
endxchar;
beginachar("s",7);
	draw (6u,.9ex)..(z1-(u,0)){left}..(0,.77ex){down}..(7u,.27ex){down}..(z3-(u,0)){left}..(.5u,.1ex);
endxchar;
beginbchar("t",5);
	draw (3u,el)..{down}(2u,1/3ex)..(2.5u,-1/8ex);
	draw (0,5/6ex){right}..(5u,ex);
endxchar;
beginachar("u",9);
	draw (u,ex)..z2{down}..z3{right}..z4--(9u,1/6ex){down}..(8.5u,-1/8ex);
endxchar;
beginachar("v",8);
	draw (0,ex){down}..z3&z3..{up}(8u,ex);
endxchar;
beginachar("w",12);
	draw (-.5u,ex){down}..(3u,.1ex)&(3u,.1ex)..(6u,5/6ex)&(6u,5/6ex)..(9u,0)&(9u,0)..{up}(12.5u,ex);
endxchar;
beginachar("x",8);
	draw (.5u,ex)..(z1+z3)/2..(7u,0);
	draw (8u,ex)..(z1+z3)/2..(0,0);
endxchar;
beginpchar("y",9,1/2);
	draw (0,ex)..z3;
	draw (9u,ex)..z3+(.5u,0)..(-2u,-1/2ex);
endxchar;
beginachar("z",8);
	draw (.5u,ex){right}..(8u,.9ex)&(8u,.9ex){dir -135}..{dir -135}(0,.1ex)&(0,.1ex)..{right}(8u,0);
endxchar;
end
